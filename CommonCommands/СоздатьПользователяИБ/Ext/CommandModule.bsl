
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Создать(ПараметрКоманды);
КонецПроцедуры

&НаСервере
Процедура Создать(ПараметрКоманды)
	Если ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Преподаватели") Тогда
		Если НЕ ПараметрКоманды.ЭтоГруппа И ЗначениеЗаполнено(ПараметрКоманды.Физлицо) Тогда
			Пользователь = Юзеры.ПользовательНайти(ПараметрКоманды.Физлицо);
			Если Пользователь = Неопределено Тогда
				Пользователь = Юзеры.ПользовательСоздать(Новый Структура("Наименование,Физлицо", ПараметрКоманды.Наименование, ПараметрКоманды.Физлицо));
			КонецЕсли;
			ЮзерСоздать(Пользователь);
		КонецЕсли;
	ИначеЕсли ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Учащиеся") Тогда
		Если НЕ ПараметрКоманды.ЭтоГруппа И ЗначениеЗаполнено(ПараметрКоманды.Физлицо) Тогда
			Пользователь = Юзеры.ПользовательНайти(ПараметрКоманды.Физлицо);
			Если Пользователь = Неопределено Тогда
				Пользователь = Юзеры.ПользовательСоздать(Новый Структура("Наименование,Физлицо", ПараметрКоманды.Наименование, ПараметрКоманды.Физлицо));
			КонецЕсли;
			ЮзерСоздать(Пользователь);
		КонецЕсли;
	Иначе	//Если ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.Пользователи") Тогда
		ЮзерСоздать(ПараметрКоманды);
	КонецЕсли;
КонецПроцедуры

Функция ЮзерСоздать(Пользователь)
	Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") И НЕ Пользователь.ЭтоГруппа Тогда
		Попытка
			УникальныйИдентификатор = Новый УникальныйИдентификатор(Пользователь.Код);
			Юзер = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(УникальныйИдентификатор);
		Исключение
			Юзер = ПользователиИнформационнойБазы.НайтиПоИмени(Пользователь.Наименование);
		КонецПопытки;
		Если Юзер = Неопределено Тогда
			Юзер = ПользователиИнформационнойБазы.СоздатьПользователя();
			Юзер.Имя		= Пользователь.Наименование;
			Юзер.ПолноеИмя	= Пользователь.Наименование;
			Юзер.АутентификацияСтандартная	= Истина;
			Юзер.Пароль						= Лев(Новый УникальныйИдентификатор, 8);
			Юзер.ПоказыватьВСпискеВыбора	= Ложь;
			//Юзер.РазделениеДанных			= Истина;
			
			Юзер.Роли.Добавить(Метаданные.Роли.Пользователь);
			
			Юзер.Записать();
		КонецЕсли;
		
		Если Пользователь.Код <> Строка(Юзер.УникальныйИдентификатор) Тогда
			ПользовательОбъект = Пользователь.ПолучитьОбъект();
			ПользовательОбъект.Код = Строка(Юзер.УникальныйИдентификатор);
			ПользовательОбъект.Записать();
		КонецЕсли;
	КонецЕсли;
КонецФункции
